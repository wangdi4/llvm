; RUN: opt %s -passes="hir-ssa-deconstruction,hir-temp-cleanup,hir-rowwise-mv,print<hir>,hir-sum-window-reuse,print<hir>" -hir-rowwise-mv-skip-dtrans -disable-output 2>&1 | FileCheck %s

; This test checks that HIRSumWindowReuse is able to optimize a multidimensional
; sliding window sum with an intermediate switch as generated by HIRRowWiseMV.

; This test also runs HIRRowWiseMV before HIRSumWindowReuse as a workaround
; because the relevant code patterns can't be reproduced otherwise in HIR.

; Print before:

; CHECK:      BEGIN REGION
; CHECK:       + DO i1 = 0, 55, 1   <DO_LOOP>
; CHECK-NEXT:       |   %sum = 0.000000e+00;
; CHECK-NEXT:       |
; CHECK-NEXT:       |   + DO i2 = 0, 15, 1   <DO_LOOP>
; CHECK-NEXT:       |   |   switch((%rwmv.rowcases)[i2])
; CHECK-NEXT:       |   |   {
; CHECK-NEXT:       |   |   case 1:
; CHECK-NEXT:       |   |      + DO i3 = 0, 7, 1   <DO_LOOP>
; CHECK-NEXT:       |   |      |   %sum = %sum  -  (%A)[i1 + 64 * i2 + i3];
; CHECK-NEXT:       |   |      + END LOOP
; CHECK-NEXT:       |   |      break;
; CHECK-NEXT:       |   |   case 2:
; CHECK-NEXT:       |   |      break;
; CHECK-NEXT:       |   |   case 3:
; CHECK-NEXT:       |   |      + DO i3 = 0, 7, 1   <DO_LOOP>
; CHECK-NEXT:       |   |      |   %AC = (%A)[i1 + 64 * i2 + i3];
; CHECK-NEXT:       |   |      |   %sum = %sum  +  %AC;
; CHECK-NEXT:       |   |      + END LOOP
; CHECK-NEXT:       |   |      break;
; CHECK-NEXT:       |   |   default:
; CHECK-NEXT:       |   |      + DO i3 = 0, 7, 1   <DO_LOOP>
; CHECK-NEXT:       |   |      |   %AC = (%C)[i2 + i3]  *  (%A)[i1 + 64 * i2 + i3];
; CHECK-NEXT:       |   |      |   %sum = %sum  +  %AC;
; CHECK-NEXT:       |   |      + END LOOP
; CHECK-NEXT:       |   |      break;
; CHECK-NEXT:       |   |   }
; CHECK-NEXT:       |   + END LOOP
; CHECK-NEXT:       |
; CHECK-NEXT:       |   (%B)[i1] = %sum;
; CHECK-NEXT:       + END LOOP
; CHECK-NEXT: END REGION

; Print after:

; CHECK:      BEGIN REGION
; CHECK:               %[[WSUM1:[A-Za-z0-9_.]+]] = 0.000000e+00;
; CHECK-NEXT:       + DO i1 = 0, 55, 1   <DO_LOOP>
; CHECK-NEXT:       |   %sum = 0.000000e+00;
; CHECK-NEXT:       |
; CHECK-NEXT:       |   + DO i2 = 0, 15, 1   <DO_LOOP>
; CHECK-NEXT:       |   |   switch((%rwmv.rowcases)[i2])
; CHECK-NEXT:       |   |   {
; CHECK-NEXT:       |   |   case 1:
; CHECK-NEXT:       |   |      if (i1 == 0)
; CHECK-NEXT:       |   |      {
; CHECK-NEXT:       |   |         + DO i3 = 0, 7, 1   <DO_LOOP>
; CHECK-NEXT:       |   |         |   %[[WSUM1]] = %[[WSUM1]]  -  (%A)[64 * i2 + i3];
; CHECK-NEXT:       |   |         + END LOOP
; CHECK-NEXT:       |   |      }
; CHECK-NEXT:       |   |      else
; CHECK-NEXT:       |   |      {
; CHECK-NEXT:       |   |         %[[WSUM1]] = %[[WSUM1]]  +  (%A)[i1 + 64 * i2 + -1];
; CHECK-NEXT:       |   |         %[[WSUM1]] = %[[WSUM1]]  -  (%A)[i1 + 64 * i2 + 7];
; CHECK-NEXT:       |   |      }
; CHECK-NEXT:       |   |      break;
; CHECK-NEXT:       |   |   case 2:
; CHECK-NEXT:       |   |      break;
; CHECK-NEXT:       |   |   case 3:
; CHECK-NEXT:       |   |      + DO i3 = 0, 7, 1   <DO_LOOP>
; CHECK-NEXT:       |   |      |   %AC = (%A)[i1 + 64 * i2 + i3];
; CHECK-NEXT:       |   |      |   %sum = %sum  +  %AC;
; CHECK-NEXT:       |   |      + END LOOP
; CHECK-NEXT:       |   |      break;
; CHECK-NEXT:       |   |   default:
; CHECK-NEXT:       |   |      + DO i3 = 0, 7, 1   <DO_LOOP>
; CHECK-NEXT:       |   |      |   %AC = (%C)[i2 + i3]  *  (%A)[i1 + 64 * i2 + i3];
; CHECK-NEXT:       |   |      |   %sum = %sum  +  %AC;
; CHECK-NEXT:       |   |      + END LOOP
; CHECK-NEXT:       |   |      break;
; CHECK-NEXT:       |   |   }
; CHECK-NEXT:       |   + END LOOP
; CHECK-NEXT:       |      %sum = %sum + %[[WSUM1]];
; CHECK-NEXT:       |
; CHECK-NEXT:       |   (%B)[i1] = %sum;
; CHECK-NEXT:       + END LOOP
; CHECK-NEXT: END REGION

target datalayout = "e-m:e-p270:32:32-p271:32:32-p272:64:64-i64:64-f80:128-n8:16:32:64-S128"
target triple = "x86_64-unknown-linux-gnu"

define void @window2d(ptr %A, ptr noalias %B, ptr %C) #0 {

entry:
  br label %L1

L1:
  %i = phi i64 [ 0, %entry ], [ %i.next, %L1.latch ]
  br label %L2

L2:
  %j = phi i64 [ 0, %L1 ], [ %j.next, %L2.latch ]
  %sum = phi double [ 0.0, %L1 ], [ %sum.next, %L2.latch ]
  %j64 = mul nsw nuw i64 %j, 64
  %ij = add nsw nuw i64 %i, %j64
  br label %L3

L3:
  %k = phi i64 [ 0, %L2 ], [ %k.next, %L3 ]
  %sum.L3 = phi double [ %sum, %L2 ], [ %sum.next.L3, %L3 ]
  %jk = add nsw nuw i64 %j, %k
  %Cjkptr = getelementptr inbounds double, ptr %C, i64 %jk
  %Cjk = load double, ptr %Cjkptr, align 8
  %ijk = add nsw nuw i64 %ij, %k
  %Aijkptr = getelementptr inbounds double, ptr %A, i64 %ijk
  %Aijk = load double, ptr %Aijkptr, align 8
  %AC = fmul fast double %Cjk, %Aijk
  %sum.next.L3 = fadd fast double %sum.L3, %AC
  %k.next = add nsw nuw i64 %k, 1
  %cond.L3 = icmp ne i64 %k.next, 8
  br i1 %cond.L3, label %L3, label %L2.latch

L2.latch:
  %sum.next = phi double [ %sum.next.L3, %L3 ]
  %j.next = add nsw nuw i64 %j, 1
  %cond.L2 = icmp ne i64 %j.next, 16
  br i1 %cond.L2, label %L2, label %L1.latch

L1.latch:
  %sum.final = phi double [ %sum.next, %L2.latch ]
  %Biptr = getelementptr inbounds double, ptr %B, i64 %i
  store double %sum.final, ptr %Biptr, align 8
  %i.next = add nsw nuw i64 %i, 1
  %cond.L1 = icmp ne i64 %i.next, 56
  br i1 %cond.L1, label %L1, label %exit

exit:
  ret void
}

attributes #0 = { "unsafe-fp-math"="true" }
