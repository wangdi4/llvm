; RUN: opt < %s -analyze -scalar-evolution | FileCheck %s

; Verify that we are able to compute backedge taken count of the loop with
; unknown lower bound and sgt backedge comparison type.

; TODO: (%n smax %lb) should be simplified to %n due to loop entry guard/ztt.
; CHECK: Loop %for.body: backedge-taken count is ((-1 * %lb) + (%n smax %lb))

target datalayout = "e-m:e-p270:32:32-p271:32:32-p272:64:64-i64:64-f80:128-n8:16:32:64-S128"
target triple = "x86_64-unknown-linux-gnu"

define dso_local void @foo(i32* nocapture %A, i32 %n, i32 %lb) {
entry:
  %cmp5 = icmp slt i32 %n, %lb
  br i1 %cmp5, label %for.end, label %for.body

for.body:                                         ; preds = %entry, %for.body
  %i.06 = phi i32 [ %inc, %for.body ], [ %lb, %entry ]
  %idxprom = zext i32 %i.06 to i64
  %arrayidx = getelementptr inbounds i32, i32* %A, i64 %idxprom
  store i32 %i.06, i32* %arrayidx, align 4
  %inc = add nuw nsw i32 %i.06, 1
  %cmp = icmp sgt i32 %inc, %n
  br i1 %cmp, label %for.end, label %for.body

for.end:                                          ; preds = %for.body, %entry
  ret void
}

