# Copyright (C) 2023 Intel Corporation
#
# This software and the related documents are Intel copyrighted materials, and
# your use of them is governed by the express license under which they were
# provided to you ("License"). Unless the License provides otherwise, you may
# not use, modify, copy, publish, distribute, disclose or transmit this software
# or the related documents without Intel's prior written permission.
#
# This software and the related documents are provided as is, with no express or
# implied warranties, other than those that are expressly stated in the License.

if (INTEL_ENABLE_PROTO_BIN_OPTRPT)
  message(FATAL_ERROR "can't use INTEL_ENABLE_PROTO_BIN_OPTRPT with external "
                      "protobuf: binary opt report uses protobufs v3.17, "
                      "while opencl-intel uses v3.21.")
endif()

if (POLICY CMP0097)
  cmake_policy(SET CMP0097 NEW)
endif ()

use_rtti(TRUE)

include(FetchContent)

#set repo
if(INTEL_CUSTOMIZATION)
  if(DEFINED ENV{ICS_GIT_MIRROR} AND NOT "$ENV{ICS_GIT_MIRROR}" STREQUAL "")
    STRING(REGEX REPLACE "\\\\" "/" GITSERVER "$ENV{ICS_GIT_MIRROR}")
	set(PROTOBUF_REPO "${GITSERVER}/protobuf.git")
  else()
    message(FATAL_ERROR "ICS_GIT_MIRROR was not found!")
  endif()
else()
  set(PROTOBUF_REPO "https://github.com/protocolbuffers/protobuf.git")
endif(INTEL_CUSTOMIZATION)

set(PROTOBUF_TAG v3.21.12)
message(STATUS "Fetch Protobuf project from ${PROTOBUF_REPO}")

set(protobuf_patch git apply ${CMAKE_CURRENT_SOURCE_DIR}/protobuf_suppress-bigobj.patch)

#get project
FetchContent_Declare(protobuf
  GIT_REPOSITORY    ${PROTOBUF_REPO}
  GIT_TAG           ${PROTOBUF_TAG}
  #supress download submodule form git
  GIT_SUBMODULES ""
  GIT_SHALLOW    TRUE
  PATCH_COMMAND ${protobuf_patch}
  UPDATE_DISCONNECTED 1
 )

#suppress tests build
set(protobuf_BUILD_TESTS OFF CACHE BOOL "disable protobuf tests" FORCE)

FetchContent_GetProperties(protobuf)
FetchContent_MakeAvailable(protobuf)

# Disable warnings generated by the protobuf source code These are taken
# from protobuf's own project
set_property(
  TARGET libprotobuf-lite libprotobuf libprotoc 
  APPEND_STRING
  PROPERTY COMPILE_FLAGS "-w ")

set_target_properties(
  protoc
  PROPERTIES 
  RUNTIME_OUTPUT_DIRECTORY "${CMAKE_CURRENT_BINARY_DIR}/bin")

#set include directory of protobuf
if(NOT EXISTS ${protobuf_SOURCE_DIR})
  message(FATAL_ERROR "Protobuf have not been fetched from ${PROTOBUF_REPO}")
else()
  set(protobuf_INCLUDE_DIR ${protobuf_SOURCE_DIR}/src CACHE STRING "")
  set(protobuf_EXECUTABLE ${CMAKE_CURRENT_BINARY_DIR}/bin/protoc CACHE STRING "")
endif()
