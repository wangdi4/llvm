# CMakeLists.txt file for unit testing Archer runtime library.
include(CheckFunctionExists)
include(CheckLibraryExists)

# When using libgcc, -latomic may be needed for atomics
# (but when using compiler-rt, the atomics will be built-in)
# Note: we can not check for __atomic_load because clang treats it
# as special built-in and that breaks CMake checks
check_function_exists(__atomic_load_1 LIBARCHER_HAVE_BUILTIN_ATOMIC)
if(NOT LIBARCHER_HAVE_BUILTIN_ATOMIC)
  check_library_exists(atomic __atomic_load_1 "" LIBARCHER_HAVE_LIBATOMIC)
else()
  # not needed
  set(LIBARCHER_HAVE_LIBATOMIC 0)
endif()

set(LIBARCHER_TEST_PATH ${CMAKE_CURRENT_SOURCE_DIR})

set(LIBARCHER_TEST_FLAGS "" CACHE STRING
  "Extra compiler flags to send to the test compiler.")

macro(pythonize_bool var)
  if (${var})
    set(${var} True)
  else()
    set(${var} False)
  endif()
endmacro()

pythonize_bool(LIBARCHER_HAVE_LIBATOMIC)
pythonize_bool(OPENMP_TEST_COMPILER_HAS_TSAN_FLAGS)

set(ARCHER_TSAN_TEST_DEPENDENCE "")
if(TARGET tsan)
  set(ARCHER_TSAN_TEST_DEPENDENCE tsan)
endif()

# INTEL_CUSTOMIZATION
# We will add this directory to LD_LIBRARY_PATH so that libraries built
# with Intel compilers can find the Intel libraries.
set(INTEL_LIB_DIR ${CMAKE_INSTALL_PREFIX}/lib)

# Archer has two test suites:
# "libarcher" uses -fopenmp to compile the tests, same as LLVM upstream.
# "libarcher-iomp" uses -qopenmp, the Intel openmp flag, to compile the tests.
foreach(ARCHER_TEST_VARIANT libarcher libarcher-iomp)
  if(ARCHER_TEST_VARIANT STREQUAL "libarcher-iomp")
    set(OPENMP_TEST_OPENMP_FLAG_ORIGINAL ${OPENMP_TEST_OPENMP_FLAG})
    set(OPENMP_TEST_OPENMP_FLAGS "-qopenmp")
    set(OPENMP_TEST_COMPILER_FEATURES_ORIGINAL ${OPENMP_TEST_COMPILER_FEATURES})
    # 'iomp' is stashed in this variable to provide a clue in configuring
    # the suite name and as a token for marking a test unsupported/xfail.
    set(OPENMP_TEST_COMPILER_FEATURES "${OPENMP_TEST_COMPILER_FEATURES} + ['iomp']")
  endif()
  set(ARCHER_TEST_BINARY_DIR ${CMAKE_CURRENT_BINARY_DIR}/${ARCHER_TEST_VARIANT})
  file(MAKE_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR})
  add_openmp_testsuite(check-${ARCHER_TEST_VARIANT} "Running ${ARCHER_TEST_VARIANT} tests" ${ARCHER_TEST_BINARY_DIR}
                       DEPENDS archer ${ARCHER_TSAN_TEST_DEPENDENCE})

  # Configure the lit.site.cfg.in file
  set(AUTO_GEN_COMMENT "## Autogenerated by ${ARCHER_TEST_VARIANT} configuration.\n# Do not edit!")
  configure_file(lit.site.cfg.in ${ARCHER_TEST_BINARY_DIR}/lit.site.cfg @ONLY)
  if(ARCHER_TEST_VARIANT STREQUAL "libarcher-iomp")
    set(OPENMP_TEST_OPENMP_FLAG ${OPENMP_TEST_OPENMP_FLAG_ORIGINAL})
    set(OPENMP_TEST_COMPILER_FEATURES ${OPENMP_TEST_COMPILER_FEATURES_ORIGINAL})
  endif()
endforeach()
# end INTEL_CUSTOMIZATION
